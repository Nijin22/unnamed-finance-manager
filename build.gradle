buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "io.ratpack:ratpack-gradle:1.7.5"
  }
}

plugins {
  id 'idea'
  id 'io.ratpack.ratpack-groovy' version '1.7.6'
  id 'jacoco' // code coverage reports
  id 'com.github.johnrengelman.shadow' version '5.2.0' // creating fat jars with all dependencies
}

group 'info.dennis_weber.unfima'
version '1.0-SNAPSHOT'

repositories {
  jcenter()
}

dependencies {
  // Logging (required for Ratpack)
  runtime "org.slf4j:slf4j-simple:1.7.25"

  // Databases
  // Note that we do use MySql drivers, even if we might use a MariaDb database. That's because Google Cloud Run
  // only works with the MySql drivers and we can't use both at the same time. MySql drivers also work for MariaDb, so
  // we simply use them.
  implementation 'mysql:mysql-connector-java:8.0.19' // MySql Drivers
  implementation 'com.google.cloud.sql:mysql-socket-factory-connector-j-8:1.0.15' // DB on Google Cloud
  implementation 'org.flywaydb:flyway-core:6.2.0' // DB migrations / upgrades to schemas via Flyway
  testCompile 'com.h2database:h2:1.4.200' // In-memory DB used during tests

  // Password hashing
  implementation 'org.mindrot:jbcrypt:0.4'

  // Test
  testCompile 'org.spockframework:spock-core:1.3-groovy-2.4'
}

application {
  mainClassName = 'info.dennis_weber.unfima.api.Main'
}

test {
  testLogging {
    exceptionFormat = 'full'
  }
}

jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled false
  }
}

// Output to build/libs/unfima-with-dependencies.jar
shadowJar {
  archiveBaseName.set('unfima-with-dependencies')
  archiveClassifier.set('')
  archiveVersion.set('')
}

